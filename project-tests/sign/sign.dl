.enum Sign = {
              case "Top",

    case "Neg", case "Zer", case "Pos",

              case "Bot"
}

/// Next, we define all the components that constitute the lattice:


/// The least upper bound relation on the lattice elements.
.def lub(x: Sign, y: Sign): Sign {
    case ("Bot", _)   => y,
    case (_, "Bot")   => x,
    case (_, _)	      => x=y ? x : "Top"
}

/// The greatest lower bound relation on the lattice elements.
.def glb(x: Sign, y: Sign): Sign {
    case ("Top", _)   => y,
    case (_, "Top")   => x,
    case (_, _)	      => x=y ? x : "Bot"
}


// use function to transfer number or symbol to enum type
.def lat_alpha(x: number): Sign {
    case (_)	      => x>0 ? "Pos" : (x<0 ? "Neg" : "Zer")
}

.def lat_sum(x: Sign, y: Sign): Sign {
    case ("Bot", _)   => "Bot",
    case (_, "Bot")   => "Bot",
    case ("Zer", _)   => y,
    case (_, "Zer")   => x,
    case (_, _)	      => x=y ? x : "Top"
}

.def lat_minus(x: Sign, y: Sign): Sign {
    case ("Bot", _)   => "Bot",
    case (_, "Bot")   => "Bot",
    case ("Top", _)   => "Top",
    case (_, "Top")   => "Top",
    case (_, "Zer")   => x,
    case ("Zer", "Neg")   => "Pos",
    case ("Zer", "Pos")   => "Neg",
    case (_, _)	      => x=y ? "Top" : x
}

.def lat_mult(x: Sign, y: Sign): Sign {
    case ("Bot", _)   => "Bot",
    case (_, "Bot")   => "Bot",
    case ("Zer", _)   => "Zer",
    case (_, "Zer")   => "Zer",
    case ("Top", _)   => "Top",
    case (_, "Top")   => "Top",
    case (_, _)	      => x=y ? "Pos" : "Neg"
}

.def lat_div(x: Sign, y: Sign): Sign {
    case ("Bot", _)   => "Bot",
    case (_, "Bot")   => "Bot",
    case ("Zer", _)   => "Zer",
    case ("Top", _)   => "Top",
    case (_, "Top")   => "Top",
    case (_, _)	      => x=y ? "Pos" : "Neg" // not very sound, could be zero
}

// assert lattice association
.let Sign<> = ("Bot", "Top", lub, glb)

.decl setConstStm(l:number, r: symbol, c: number)         // r = c
.input setConstStm
.decl addStm(l:number, r: symbol, x: symbol, y: symbol) // r = x + y
.input addStm
.decl minusStm(l:number, r: symbol, x: symbol, y: symbol) // r = x - y
.input minusStm
.decl multStm(l:number, r: symbol, x: symbol, y: symbol) // r = x * y
.input multStm
.decl divStm(l:number, r: symbol, x: symbol, y: symbol) // r = x / y
.input divStm

.decl flow(l1: number, l2: number) // control flow from l1 to l2
.input flow

.lat varEntry(l:number, k: symbol, v: Sign)
.output varEntry
.lat varExit(l:number, k: symbol, v: Sign)
.output varExit

// varEntry of l2 is the union of {varExit(l1) | flow(l1,l2)}
varEntry(l2, k, v) :- varExit(l1, k, v), flow(l1, l2).

// statement: set to constant number
varExit(l, r, &lat_alpha(c)) :- setConstStm(l, r, c).

// addition statement r = x+y, and the value of x is v1, the
// value of y is v2
varExit(l, r, &lat_sum(v1, v2)) :- addStm(l, r, x, y),
                            varEntry(l, x, v1),
                            varEntry(l, y, v2).
// division statement: r = x - y
varExit(l, r, &lat_minus(v1, v2)) :- minusStm(l, r, x, y),
                            varEntry(l, x, v1),
                            varEntry(l, y, v2).
// multiplication statement: r = x * y
varExit(l, r, &lat_mult(v1, v2)) :- multStm(l, r, x, y),
                            varEntry(l, x, v1),
                            varEntry(l, y, v2).
// division statement: r = x / y
varExit(l, r, &lat_div(v1, v2)) :- divStm(l, r, x, y),
                            varEntry(l, x, v1),
                            varEntry(l, y, v2), v2!=&lat_alpha(0).

// if the statement doesn't assign to r
.decl assignVar(l:number, r: symbol) // this statement assign r to a new value
assignVar(l, r) :- setConstStm(l, r, _).
assignVar(l, r) :- addStm(l, r, _, _).
assignVar(l, r) :- minusStm(l, r, _, _).
assignVar(l, r) :- multStm(l, r, _, _).
assignVar(l, r) :- divStm(l, r, _, _).
varExit(l, r, v) :- varEntry(l, r, v), !assignVar(l, r).

